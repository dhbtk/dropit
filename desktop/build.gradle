buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
    id "org.flywaydb.flyway" version "5.2.4"
    id 'nu.studer.jooq' version '3.0.2'
}

description = ''
sourceCompatibility = 1.8
targetCompatibility = 1.8
def buildDbPath = '/tmp/dropit-tmp.db'

dependencies {
    compile project(':common')
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'
    jooqRuntime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'

    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.7.8'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.0.7'
    compile group: 'org.jooq', name: 'jooq'
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.22'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'com.google.dagger', name: 'dagger', version: '2.19'
    compile group: 'io.javalin', name: 'javalin', version: '2.4.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.2'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.3'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.3.11'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.11'

    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: '1.3.11'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'
    compile(group: 'org.spekframework.spek2', name: 'spek-runner-junit5', version: '2.0.0-alpha.2') {
        exclude(module: '*')
        exclude(module: '*')
    }
    testCompile(group: 'org.spekframework.spek2', name: 'spek-dsl-jvm', version: '2.0.0-alpha.2') {
        exclude(module: '*')
    }

    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: '2.19'
}

flyway {
    url = "jdbc:sqlite:$buildDbPath"
}

jooq {
    desktop(sourceSets.main) {
        jdbc {
            driver = 'org.sqlite.JDBC'
            url = "jdbc:sqlite:$buildDbPath"
        }
        generator {
            database {
                includes = '.*'
                excludes = 'flyway_schema_history'
                includeSequences = false
                includePrimaryKeys = false
                includeUniqueKeys = false
                includeForeignKeys = false
            }
            target {
                packageName = 'dropit.jooq'
            }
        }
    }
}

tasks.generateDesktopJooqSchemaSource.dependsOn(flywayMigrate)

tasks.compileKotlin.dependsOn(generateDesktopJooqSchemaSource)