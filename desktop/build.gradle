import java.nio.file.Files

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
    id 'nu.studer.jooq' version '3.0.2'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'application'
    id 'edu.sc.seis.launch4j' version '2.4.4'
    id "edu.sc.seis.macAppBundle" version "2.3.0"
    id 'idea'
}

description = ''
sourceCompatibility = 1.8
targetCompatibility = 1.8


mainClassName = 'dropit.ApplicationKt'
applicationName = 'DropIt'

shadowJar {
    baseName = 'dropit-desktop'
    classifier = project.ext.desktopClassifier
}

launch4j {
    icon = "${projectDir}/desktop.ico"
    outfile = "${project.applicationName}.exe"
    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    bundledJrePath = "jre"
}

macAppBundle {
    javaExtras.put("-XstartOnFirstThread", null)
    javaExtras.put("-d64", null)
    mainClassName = 'dropit.ApplicationKt'
    appName = applicationName
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

idea {
    module {
        sourceDirs += files('build/generated/source/kapt/main')
        generatedSourceDirs += files('build/generated/source/kapt/main')
    }
}

dependencies {
    compile project(':server')
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logback_version
    compile group: 'com.google.dagger', name: 'dagger', version: dagger_version
    compile "io.arrow-kt:arrow-core-data:$arrow_version"
//    compile "io.arrow-kt:arrow-core-extensions:$arrow_version"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_jupiter_version
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
    testImplementation ("org.spekframework.spek2:spek-dsl-jvm:$spek_version")  {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly ("org.spekframework.spek2:spek-runner-junit5:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    // UI
    compile("org.eclipse.platform:org.eclipse.swt.$swt_artifact:$swt_version") {
        exclude group: 'org.eclipse.platform'
    }
    compile("org.eclipse.platform:org.eclipse.swt:$swt_version") {
        exclude group: 'org.eclipse.platform'
    }
    compile 'com.github.kenglxn.QRGen:javase:2.6.0'

    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: dagger_version
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
}
